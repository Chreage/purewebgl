precision mediump float;

uniform vec3 lightDir, fogColor;
uniform vec2 scaleUV;
uniform sampler2D sampler, samplerNormals, samplerRivers;

varying float vFog, vHeight;
varying vec2 vUV;
varying vec3 vNormal;


const vec3 Z=vec3(0.,0.,1.);

//normal mapping : retourne la matrice de rotation d'axe u et d'angle theta
mat3 rot_angleAxe(float theta, vec3 u) {
    float c=cos(theta);
    float s=sin(theta);
    return mat3(
        u.x*u.x+(1.-u.x*u.x)*c,  u.x*u.y*(1.-c)-u.z*s,    u.x*u.z*(1.-c)+u.y*s,
        u.x*u.y*(1.-c)+u.z*s,    u.y*u.y+(1.-u.y*u.y)*c,  u.y*u.z*(1.-c)-u.x*s,
        u.x*u.z*(1.-c)-u.y*s,    u.y*u.z*(1.-c)+u.x*s,    u.z*u.z+(1.-u.z*u.z)*c
    );
}

//normal mapping : retourne la matrice de rotation qui envoie u sur v. u et v sont unitaires
mat3 rot_vectVect(vec3 u,vec3 v) {
    vec3 axe=normalize(cross(v,u));
    float theta=acos(dot(u,v));
    return rot_angleAxe(theta, axe);
}


void main(void) {
    vec4 colorRiver=texture2D(samplerRivers, vUV);

    vec2 UVscaled=vUV*scaleUV;
    vec4 color=texture2D(sampler, UVscaled);
    vec4 texture_normale=texture2D(samplerNormals, UVscaled);
    vec3 n=vec3(2.*(texture_normale.x-0.5), 2.*(texture_normale.y-0.5), texture_normale.z);
    mat3 rot=rot_vectVect(Z, vNormal);
    //vec3 bump_normal=rot*n;
    vec3 bump_normal=mix(rot*n, vec3(0.,0.,1.1), min(1., colorRiver.a*4.));


    //light intensity
    float I=dot(bump_normal,lightDir);
    //float I=dot(vNormal,lightDir);

   
    vec3 col=I*mix(color.rgb, colorRiver.rgb, colorRiver.a);
   
    //compute alpha to mix water river and sea water near the seashore
    float alpha=mix(1., vHeight,min(1.,16.*colorRiver.a));
    //float alpha=vHeight;
    gl_FragColor =  vec4(mix(fogColor, col, vFog), alpha);
    //gl_FragColor = vec4(alpha,0.,0.,1.);
}